{-# LANGUAGE MultiWayIf #-}
{-# LANGUAGE ScopedTypeVariables #-}
{-# LANGUAGE StrictData #-}
{-# LANGUAGE TupleSections #-}
{-# LANGUAGE TypeApplications #-}

module Cardano.Node.Configuration.CLIArgs
  ( -- * CLI Arguments
    CLIArgs (..)
  , parseCLIArgs
  , ShutdownOn (..)

    -- * Tracing
  , TracerConnectionRole (..)
  , TracerConnectionMethod (..)
  , TracerConnection (..)

    -- * Credentials
  , Credentials (..)
  ) where

import Cardano.Ledger.BaseTypes (BlockNo (..), SlotNo (..))
import Cardano.Node.Configuration.Common
import Control.Monad (when)
import Data.Bifunctor (second)
import Data.Char (isDigit)
import Data.IP (IPv4, IPv6)
import Data.Text (Text)
import qualified Data.Text as Text
import Network.Socket (PortNumber)
import Options.Applicative
import System.Posix.Types (Fd (..))
import Text.Read (readEither, readMaybe)

data ShutdownOn
  = ShutdownAtSlot SlotNo
  | ShutdownAtBlock BlockNo
  deriving Show

data TracerConnectionRole
  = Accept
  | Connect
  deriving Show

type Host = Text

data TracerConnectionMethod
  = TracerConnectViaPipe FilePath
  | TracerConnectViaRemote Host PortNumber
  deriving Show

data TracerConnection
  = TracerConnection TracerConnectionRole TracerConnectionMethod
  deriving Show

data Credentials = Credentials
  { byronDelegationCertificate :: Maybe FilePath
  , byronSigningKey :: Maybe FilePath
  , shelleyKESKey :: Maybe FilePath
  , shelleyVRFKey :: Maybe FilePath
  , shelleyOperationalCertificate :: Maybe FilePath
  , bulkCredentialsFile :: Maybe FilePath
  }
  deriving Show

data CLIArgs = CLIArgs
  { configFilePath :: FilePath
  , topologyFile :: FilePath
  , databasePathCLI :: Maybe NodeDatabasePaths
  , validateDatabase :: Bool
  , socketPath :: Maybe FilePath
  , credentials :: Credentials
  , startAsNonProducingNode :: Maybe Bool
  , hostAddr :: Maybe IPv4
  , hostIPv6Addr :: Maybe IPv6
  , port :: Maybe PortNumber
  , tracerSocket :: Maybe TracerConnection
  , shutdownIPC :: Maybe Fd
  , shutdownOnTarget :: Maybe ShutdownOn
  }
  deriving Show

parseCredentials :: Parser Credentials
parseCredentials =
  Credentials
    <$> optional parseByronDelegationCert
    <*> optional parseByronSigningKey
    <*> optional parseKesKeyFilePath
    <*> optional parseVrfKeyFilePath
    <*> optional parseOperationalCertFilePath
    <*> optional parseBulkCredsFilePath

parseCLIArgs :: Parser CLIArgs
parseCLIArgs =
  CLIArgs
    <$> parseConfigFile
    <*> parseTopologyFile
    <*> parserOptionGroup "Storage:" parseNodeDatabasePaths
    <*> parserOptionGroup "Storage:" parseValidateDB
    <*> optional parseSocketPath
    <*> parserOptionGroup "Credentials:" parseCredentials
    <*> parserOptionGroup "Credentials:" parseStartAsNonProducingNode
    <*> parserOptionGroup "Host:" (optional parseHostIPv4Addr)
    <*> parserOptionGroup "Host:" (optional parseHostIPv6Addr)
    <*> parserOptionGroup "Host:" (optional parsePort)
    <*> parserOptionGroup "Tracing:" (optional parseTracerSocketMode)
    <*> parserOptionGroup "Shutdown:" (optional parseShutdownIPC)
    <*> parserOptionGroup "Shutdown:" (optional parseShutdownOn)

parseTopologyFile :: Parser FilePath
parseTopologyFile =
  strOption $
    mconcat
      [ long "topology"
      , metavar "FILEPATH"
      , help "The path to a file describing the topology"
      , completer (bashCompleter "file")
      , value "configuration/cardano/mainnet-topology.json"
      , showDefault
      ]

parseValidateDB :: Parser Bool
parseValidateDB =
  switch $
    mconcat
      [ long "validate-db"
      , help "Validate all on-disk database files"
      ]

parseSocketPath :: Parser FilePath
parseSocketPath =
  strOption $
    mconcat
      [ long "socket-path"
      , help "Path to create a socket for local clients"
      , metavar "FILEPATH"
      , completer (bashCompleter "file")
      ]

parseConfigFile :: Parser FilePath
parseConfigFile =
  strOption $
    mconcat
      [ long "config"
      , metavar "FILEPATH"
      , help "Configuration file for the cardano-node"
      , completer (bashCompleter "file")
      , value "configuration/cardano/mainnet-config.json"
      , showDefault
      ]

parseHostIPv4Addr :: Parser IPv4
parseHostIPv4Addr =
  option
    (eitherReader parseNodeHostIPv4Address)
    $ mconcat
      [ long "host-addr"
      , metavar "IPV4"
      , help "An optional IPv4 address"
      ]

parseHostIPv6Addr :: Parser IPv6
parseHostIPv6Addr =
  option
    (eitherReader parseNodeHostIPv6Address)
    $ mconcat
      [ long "host-ipv6-addr"
      , metavar "IPV6"
      , help "An optional IPv6 address"
      ]

parseNodeHostIPv4Address :: String -> Either String IPv4
parseNodeHostIPv4Address s =
  maybe
    ( Left $
        "Failed to parse IPv4 address: "
          ++ s
          ++ ". If you want to specify an IPv6 address, use --host-ipv6-addr option."
    )
    Right
    (readMaybe s)

parseNodeHostIPv6Address :: String -> Either String IPv6
parseNodeHostIPv6Address s =
  maybe
    ( Left $
        "Failed to parse IPv6 address: "
          ++ s
          ++ ". If you want to specify an IPv4 address, use --host-addr option."
    )
    Right
    (readMaybe s)

parsePort :: Parser PortNumber
parsePort =
  option
    (fromIntegral <$> auto @Int)
    $ mconcat
      [ long "port"
      , metavar "PORT"
      , help "The port number"
      , value 0 -- Use an ephemeral port
      ]

parseByronDelegationCert :: Parser FilePath
parseByronDelegationCert =
  strOption $
    mconcat
      [ long "byron-delegation-certificate"
      , metavar "FILEPATH"
      , help "Path to the delegation certificate"
      , completer (bashCompleter "file")
      ]

parseByronSigningKey :: Parser FilePath
parseByronSigningKey =
  strOption $
    mconcat
      [ long "byron-signing-key"
      , metavar "FILEPATH"
      , help "Path to the Byron signing key"
      , completer (bashCompleter "file")
      ]

parseOperationalCertFilePath :: Parser FilePath
parseOperationalCertFilePath =
  strOption $
    mconcat
      [ long "shelley-operational-certificate"
      , metavar "FILEPATH"
      , help "Path to the delegation certificate"
      , completer (bashCompleter "file")
      ]

parseBulkCredsFilePath :: Parser FilePath
parseBulkCredsFilePath =
  strOption $
    mconcat
      [ long "bulk-credentials-file"
      , metavar "FILEPATH"
      , help "Path to the bulk pool credentials file"
      , completer (bashCompleter "file")
      ]

-- TODO: pass the current KES evolution, not the KES_0
parseKesKeyFilePath :: Parser FilePath
parseKesKeyFilePath =
  strOption $
    mconcat
      [ long "shelley-kes-key"
      , metavar "FILEPATH"
      , help "Path to the KES signing key"
      , completer (bashCompleter "file")
      ]

parseVrfKeyFilePath :: Parser FilePath
parseVrfKeyFilePath =
  strOption $
    mconcat
      [ long "shelley-vrf-key"
      , metavar "FILEPATH"
      , help "Path to the VRF signing key"
      , completer (bashCompleter "file")
      ]

parseNodeAddress :: ReadM (Host, PortNumber)
parseNodeAddress = eitherReader parseHostPort

parseHostPort :: String -> Either String (Host, PortNumber)
parseHostPort s
  | (portRev, ':' : hostRev) <- break (== ':') (reverse s) =
      if
        | null hostRev -> Left "parseHostPort: Empty host."
        | null portRev -> Left "parseHostPort: Empty port."
        | all isDigit portRev
        , Just p <- readMaybe @PortNumber (reverse portRev) ->
            if
              | 0 <= p, p <= 65535 -> Right (Text.pack (reverse hostRev), p)
              | otherwise -> Left ("parseHostPort: Numeric port '" ++ show p ++ "' out of range: 0 - 65535)")
        | otherwise -> Left "parseHostPort: Non-numeric port."
  | otherwise =
      Left "parseHostPort: No colon found."

parseTracerSocketMode :: Parser TracerConnection
parseTracerSocketMode =
  fmap (uncurry TracerConnection) $
    fmap
      (second (uncurry TracerConnectViaRemote))
      ( asum
          [ fmap (Accept,) $
              option parseNodeAddress $
                mconcat
                  [ long "tracer-socket-network-accept"
                  , help "Accept incoming cardano-tracer connection on HOST:PORT"
                  , metavar "HOST:PORT"
                  ]
          , fmap (Connect,) $
              option parseNodeAddress $
                mconcat
                  [ long "tracer-socket-network-connect"
                  , help "Connect to cardano-tracer listening on HOST:PORT"
                  , metavar "HOST:PORT"
                  ]
          ]
      )
      <|> fmap
        (second TracerConnectViaPipe)
        ( asum
            [ fmap (Accept,) $
                strOption $
                  mconcat
                    [ long "tracer-socket-path-accept"
                    , help "Accept incoming cardano-tracer connection at local socket"
                    , completer (bashCompleter "file")
                    , metavar "FILEPATH"
                    ]
            , fmap (Connect,) $
                strOption $
                  mconcat
                    [ long "tracer-socket-path-connect"
                    , help "Connect to cardano-tracer listening on a local socket"
                    , completer (bashCompleter "file")
                    , metavar "FILEPATH"
                    ]
            ]
        )

parseShutdownIPC :: Parser Fd
parseShutdownIPC =
  option
    (Fd <$> auto)
    $ mconcat
      [ long "shutdown-ipc"
      , metavar "FD"
      , help "Shut down the process when this inherited FD reaches EOF"
      , hidden
      ]

parseShutdownOn :: Parser ShutdownOn
parseShutdownOn =
  asum
    [ option (ShutdownAtSlot . SlotNo <$> bounded "SLOT") $
        mconcat
          [ long "shutdown-on-slot-synced"
          , metavar "SLOT"
          , help "Shut down the process after ChainDB is synced up to the specified slot"
          , hidden
          ]
    , option (ShutdownAtBlock . BlockNo <$> bounded "BLOCK") $
        mconcat
          [ long "shutdown-on-block-synced"
          , metavar "BLOCK"
          , help "Shut down the process after ChainDB is synced up to the specified block"
          , hidden
          ]
    ]
 where
  bounded :: forall a. (Bounded a, Integral a, Show a) => String -> ReadM a
  bounded t = eitherReader $ \s -> do
    i <- readEither @Integer s
    when (i < fromIntegral (minBound @a)) $ Left $ t <> " must not be less than " <> show (minBound @a)
    when (i > fromIntegral (maxBound @a)) $ Left $ t <> " must not greater than " <> show (maxBound @a)
    pure (fromIntegral i)
