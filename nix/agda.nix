inputs: final: prev:

let
  pkgs = final;

  locales = {
    LANG = "en_US.UTF-8";
    LC_ALL = "en_US.UTF-8";
    LOCALE_ARCHIVE =
      if pkgs.system == "x86_64-linux"
      then "${pkgs.glibcLocales}/lib/locale/locale-archive"
      else "";
  };

  customAgda = inputs.agda-nixpkgs.legacyPackages.${pkgs.system};

  agdaStdlib = customAgda.agdaPackages.standard-library;

  agdaStdlibClasses = customAgda.agdaPackages.mkDerivation {
    inherit (locales) LANG LC_ALL LOCALE_ARCHIVE;
    pname = "agda-stdlib-classes";
    version = "2.3";
    src = pkgs.fetchFromGitHub {
      owner = "agda";
      repo = "agda-stdlib-classes";
      rev = "v2.3";
      sha256 = "0bbgc3nf1b2v3wljrq7974z38apzzsdhfzc1fdmm4fsmnpglmb1m";
    };
    meta = { };
    libraryFile = "agda-stdlib-classes.agda-lib";
    everythingFile = "standard-library-classes.agda";
    buildInputs = [ agdaStdlib ];
  };

  agdaStdlibMeta = customAgda.agdaPackages.mkDerivation {
    inherit (locales) LANG LC_ALL LOCALE_ARCHIVE;
    pname = "agda-stdlib-meta";
    version = "2.3";
    src = pkgs.fetchFromGitHub {
      owner = "agda";
      repo = "agda-stdlib-meta";
      rev = "v2.3";
      sha256 = "1n41cfkahg2zzfm113dkqlh5m07rvm9jjh8ps50qi3cpkz203gla";
    };
    meta = { };
    libraryFile = "agda-stdlib-meta.agda-lib";
    everythingFile = "standard-library-meta.agda";
    buildInputs = [ agdaStdlib agdaStdlibClasses ];
  };

  agdaSets = customAgda.agdaPackages.mkDerivation {
    inherit (locales) LANG LC_ALL LOCALE_ARCHIVE;
    pname = "agda-sets";
    version = "+";
    src = pkgs.fetchFromGitHub {
      owner = "input-output-hk";
      repo = "agda-sets";
      rev = "31512b000317a577230e9ba5081b693801104851";
      sha256 = "1yj8a8r17y1pld87329cjvmfnha7ih5zan3wccc3sq661apr17l8";
    };
    meta = { };
    libraryFile = "abstract-set-theory.agda-lib";
    everythingFile = "src/abstract-set-theory.agda";
    buildInputs = [ agdaStdlib agdaStdlibClasses agdaStdlibMeta ];
  };

  agdaIOGPrelude = customAgda.agdaPackages.mkDerivation {
    inherit (locales) LANG LC_ALL LOCALE_ARCHIVE;
    pname = "agda-prelude";
    version = "2.0";
    src = pkgs.fetchFromGitHub {
      repo = "iog-agda-prelude";
      owner = "input-output-hk";
      rev = "e25670dcea694f321cbcd7a0bb704b82d5d7b266";
      sha256 = "1r2g7akia33yis8kgw398w0z484zry1q26739wcq6dfdyw7zb8v7";
    };
    meta = { };
    libraryFile = "iog-prelude.agda-lib";
    everythingFile = "src/Everything.agda";
    buildInputs = [ agdaStdlib agdaStdlibClasses agdaStdlibMeta ];
  };

  deps = [ agdaStdlib agdaStdlibClasses agdaStdlibMeta agdaSets agdaIOGPrelude ];
  agdaWithPkgs = p: customAgda.agda.withPackages { pkgs = p; };

  attrs = pkgs.recurseIntoAttrs rec {
    agda = agdaWithPkgs deps;

    latex = pkgs.texlive.combine {
      inherit (pkgs.texlive)
        scheme-small
        xits
        collection-latexextra
        collection-latexrecommended
        collection-mathscience
        bclogo
        latexmk;
    };

    docs = pkgs.stdenv.mkDerivation {
      inherit (locales) LANG LC_ALL LOCALE_ARCHIVE;
      pname = "docs";
      version = "0.1";
      src = ../docs/agda-spec;
      meta = { };
      buildInputs = [ agda latex pkgs.python3 ];
      buildPhase = ''
        OUT_DIR=$out make docs
      '';
      doCheck = true;
      checkPhase = ''
        test -n "$(find $out/pdfs/ -type f -name '*.pdf')"
      '';
      dontInstall = true;
    };

    # this derivation generates the Haskell code from the Agda spec, but does
    # not build or test it.
    # TODO(georgy.lukyanov): share agda typechecking with the docs derivation
    hsSrc = pkgs.stdenv.mkDerivation {
      inherit (locales) LANG LC_ALL LOCALE_ARCHIVE;
      pname = "agda-spec-hs-src";
      version = "0.1";
      src = ../docs/agda-spec;
      meta = { };
      buildInputs = [ agda customAgda.ghc customAgda.cabal-install ];
      buildPhase = ''
        OUT_DIR=$out make hs
      '';
      doCheck = true;
      checkPhase = ''
        test -n "$(find $out/haskell/ -type f -name '*.hs')"
      '';
      dontInstall = true;
    };

    # this derivation picks up the Haskell project generated by hsSrc and
    # builds and tests it. Note that the profiled build is intentionally disabled
    # as is causes some GHC versions to panic.
    hsExe = customAgda.haskell.lib.disableLibraryProfiling (customAgda.haskellPackages.callCabal2nixWithOptions "cardano-consensus-executable-spec" "${hsSrc}/haskell/Spec" "--no-haddock" { });

    shell = pkgs.mkShell {
      packages = [ agda latex customAgda.ghc customAgda.cabal-install ];
    };
  };
in
{ agda-spec = attrs; }
